scn newDurabilityScript

short bDisableDurability
float fWeaponBaseHealth
float fWeaponSkillMult
float fArmorBaseHealth
float fArmorSkillMult

Begin MenuMode 4
    if eval (GetGameRestarted)
        if eval (!FileExists "config/NewDurability.ini")
            print "'data/config/NewDurabitily.ini' not found!"
        else
            if eval (GetINIFloat "DEBUG:bEnablePrints", "NewDurability.ini")
                DBMode 1, (GetModIndex "NewDurability.esp")
            else
                DBMode 0, (GetModIndex "NewDurability.esp")
            endif

            let bDisableDurability := GetINIFloat "EXTRAS:bDisableDurability", "NewDurability.ini"
            if eval (bDisableDurability)
                PrintD "NewDurability: Disabling durability..."
                SetNumericGameSetting "fDamageToArmorPercentage", 0
                SetNumericGameSetting "fDamageToWeaponValue", 0
                ; NOTE: When you uninstall the mod, this changes are reverted by default.
            else
                PrintD "NewDurability: Enabling durability..."
                let fWeaponBaseHealth := Clamp (GetINIFloat "WEAPONS:fWeaponBaseHealth", "NewDurability.ini"), 0, 3
                let fWeaponSkillMult := Clamp (GetINIFloat "WEAPONS:fWeaponSkillMult", "NewDurability.ini"), 0, 3
                let fArmorBaseHealth := Clamp (GetINIFloat "ARMORS:fArmorBaseHealth", "NewDurability.ini"), 0, 3
                let fArmorSkillMult := Clamp (GetINIFloat "ARMORS:fArmorSkillMult", "NewDurability.ini"), 0, 3
                DPrintV fWeaponBaseHealth, fWeaponSkillMult, fArmorBaseHealth, fArmorSkillMult
            endif
        endif

        SetEventHandler "PostLoadGame", newDurabilityPostLoadGame
        StopQuest newDurabilityQuest ; TODO: Check if, after stopping, the variables can be used in other scripts
    endif
End