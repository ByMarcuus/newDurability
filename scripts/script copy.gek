scn newDurabilityScript

ref weapon

float getter
float flag

short bDisableDurability


; Initialization script that runs on HUDMainMenu.
; Loads some configurations once.
Begin MenuMode 4
    if (GetGameRestarted != 0)
        if (FileExists "config/newDurability.ini" == 0)
            printc "NewDurability: config/newDurability.ini not found!"
        else
            if (GetINIFloat "DEBUG:bEnablePrints", "newDurability.ini")
                DBMode 1, (GetModIndex "NewDurability.esp")
            else
                DBMode 0, (GetModIndex "NewDurability.esp")
            endif

            if (GetINIFloat "EXTRAS:bDisableDurability")
                dbprintc "NewDurability: Disabling durability..."
                set bDisableDurability to 1
                SetNumericGameSetting "fDamageToArmorPercentage", 0
                SetNumericGameSetting "fDamageToWeaponValue", 0
            else
                dbprintc "NewDurability: Enabling durability..."
                set getter to (GetINIFloat "WEAPONS:fWeaponBaseHealth", "newDurability.ini")
                if (getter >= 0)
                    if (getter <= 3)
                        SetGlobalVariable fWeaponBaseHealth getter
                        dbprintc "NewDurability: Set fWeaponBaseHealth (%.2f)", getter
                    endif
                endif

                set getter to (GetINIFloat "ARMORS:fArmorBaseHealth", "newDurability.ini")
                if (getter >= 0)
                    SetGlobalVariable fArmorBaseHealth getter
                    dbprintc "NewDurability: Set fArmorBaseHealth (%.2f)", getter
                endif

                set getter to (GetINIFloat "WEAPONS:fWeaponSkillMult", "newDurability.ini")
                if (getter >= 0)
                    if (getter <= 3)
                        SetGlobalVariable fWeaponSkillMult getter
                        dbprintc "NewDurability: Set fWeaponSkillMult (%.2f)", getter
                    endif
                endif

                set getter to (GetINIFloat "ARMORS:fArmorSkillMult", "newDurability.ini")
                if (getter >= 0)
                    if (getter <= 3)
                        SetGlobalVariable fArmorSkillMult getter
                        dbprintc "NewDurability: Set fArmorSkillMult (%.2f)", getter
                    endif
                endif

                set getter to 0
            endif
        endif
    endif
End

; Script that disables OverrideDamageToWeapon Flag
Begin GameMode
    if (GetGameLoaded != 0)
        dbprintc "NewDurability: Disabling 'Override DamageToWeaponMult' Flag..."
        GetLoadedType newDurabilityList, 40

        Label 1
        if (ListGetCount newDurabilityList != 0)
            set weapon to ListGetNthForm newDurabilityList, 0
            ListRemoveNthForm newDurabilityList, 0
            if (IsQuestItem weapon == 1)
                Goto 1
            endif

            ; FIX: Disable DamageToWeapon Flag
            set flag to GetWeaponFlags2 weapon
            if ((flag % 256) >= 128)
                set flag to flag - 128
                SetWeaponFlags2 flag, weapon
            endif
            Goto 1
        endif
        set weapon to 0
        set flag to 0

        if (bDisableDurability == 1)
            dbprintc "NewDurability: Disabling durability logic..."
            StopQuest newDurabilityLogic
            StopQuest newDurability
        endif
    endif
End

scn newDurabilityLogicScript

ref armor
ref weapon
ref lastWeapon

float durability
float rating
float lastRating
int skill


; Restart the durability at 'LevelUp Menu', 'cause the special/skills can change.
Begin MenuMode 1027
	if (lastWeapon != 0 || lastRating != 0)
		set lastWeapon to 0
		set lastRating to 0
	endif
End

; Script that modifies in-game weapon and armor durability.
Begin GameMode
	set weapon to Player.GetEqObj 5
	set rating to Player.GetArmorRating

	if (weapon == 0)
		; pass
	elseif (weapon != lastWeapon)
		set lastWeapon to weapon
		set skill to GetWeaponSkill weapon

		if (skill == 38)
			set durability to Player.GetAV MeleeWeapons
		elseif (skill == 45)
			set durability to Player.GetAV Unarmed
		elseif (skill == 41)
			set durability to Player.GetAV SmallGuns
		elseif (skill == 34)
			set durability to Player.GetAV EnergyWeapons
		elseif (skill == 33)
			set durability to Player.GetAV BigGuns
		else
			set skill to 0
		endif

		if (skill != 0)
			set durability to fWeaponBaseHealth + (((100 - durability) / 100) * fWeaponSkillMult)
			dbprintc "Weapon durability: %.5f", durability
			set durability to durability / GetDamage weapon ; BUG: Float loss precision

			; FIX: Energy 'Small Gun'-like
			; Establishes the durability for all types of weapons.
			; This is done because it seems that some weapons are not attached into the correct 'fDamage*' variable.
			SetNumericGameSetting "fDamageToWeaponGunMult", durability
			SetNumericGameSetting "fDamageToWeaponEnergyMult", durability
			SetNumericGameSetting "fDamageToWeaponMeleeMult", durability
			SetNumericGameSetting "fDamageToWeaponLauncherMult", durability
		endif
	endif

	if (rating == 0)
		; pass
	elseif (rating != lastRating)
		set lastRating to rating
		set armor to Player.GetEqObj 2 ; Upper Body Piece
		set skill to GetWeight armor

		if (IsPA armor || skill >= 25)
			set durability to Player.GetAV Strength
		elseif (skill >= 15)
			set durability to Player.GetAV Endurance
		else
			set durability to Player.GetAV Agility
		endif

		set durability to fArmorBaseHealth + (((10 - durability) / 10) * fArmorSkillMult)
		dbprintc "Armor durability: %.5f", durability
		set durability to durability / lastRating ; BUG: Float loss precision
		SetNumericGameSetting "fDamageToArmorPercentage", durability
	endif
End