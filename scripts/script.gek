scn newDurabilityScript

; GLOBAL VARIABLES
float baseWeapon
float baseArmor
int multWeapon
int multArmor

; LOCAL VARIABLES
float flag
int index

ref armor
ref weapon
ref lastWeapon

float durability
float rating

Begin MenuMode

	set baseWeapon to 0.3
	set baseArmor to 1.5

	set multWeapon to 200
	set multArmor to 1

	DBMode 0, (GetModIndex "NewDurability.esp")

	if (FileExists "config/newDurability.ini")
		if (GetINIFloat "DEBUG:bEnable", "newDurability.ini")
			DBMode 1, (GetModIndex "NewDurability.esp")
		endif

		if (GetINIFloat "EXTRAS:bDisableDurability")
			SetNumericGameSetting "fDamageToArmorPercentage", 0
			SetNumericGameSetting "fDamageToWeaponGunMult", 0
			SetNumericGameSetting "fDamageToEnergyGunMult", 0
			SetNumericGameSetting "fDamageToMeleeGunMult", 0
			SetNumericGameSetting "fDamageToWeaponLauncherMul", 0
		endif

		set baseWeapon to (GetINIFloat "PARAMS:fWeaponBaseHealth", "newDurability.ini")
		if (baseWeapon <= 0)
			set baseWeapon to 0.3
		endif

		set baseArmor to (GetINIFloat "PARAMS:fArmorBaseHealth", "newDurability.ini")
		if (baseArmor <= 0)
			set baseArmor to 1.5
		endif

		if (GetINIFloat "PARAMS:fWeaponSkillMult", "newDurability.ini")
			set multWeapon to (GetINIFloat "PARAMS:fWeaponSkillMult", "newDurability.ini") * 100
		endif

		if (GetINIFloat "PARAMS:fArmorSkillMult", "newDurability.ini")
			set multArmor to (GetINIFloat "PARAMS:fArmorSkillMult", "newDurability.ini")
		endif

	endif
End

; Credits
; https://geckwiki.com

Begin MenuMode 1004 ; HUDMainMenu

	GetLoadedType newDurabilityList, 40
	set index to (ListGetCount newDurabilityList - 1)

	Label 1
	if (index >= 0)

		set weapon to ListGetNthForm newDurabilityList, index

		; FIX: Disable DamageToWeapon Flag
		set flag to GetWeaponFlags2 weapon
		if (flag >= 128 && IsQuestItem weapon == 0)
			set flag to (((((((flag / 2) / 2) / 2) / 2) / 2) / 2) / 2)
			set flag to ((flr flag) % 2)

			if (flag)
				set flag to (GetWeaponFlags2 weapon - 128)
				SetWeaponFlags2 flag, weapon
			endif
		endif

		set index to index - 1
		Goto 1
	endif

	ListClear newDurabilityList
	set weapon to 0
	set flag to 0

	if (GetINIFloat "EXTRAS:bDisableDurability")
		StopQuest newDurability
		return
	endif
End

Begin MenuMode 1027 ; LevelUp Menu
	set rating to 0
	set lastWeapon to 0
End

Begin GameMode ; Self-explanatory

	set weapon to Player.GetEqObj 5

	if (weapon && weapon != lastWeapon)
		set lastWeapon to weapon

		if (GetWeaponSkill weapon == 38)
			set durability to ((baseWeapon + ((100 - Player.GetAV MeleeWeapons) / multWeapon)) * (GetHealth weapon / 100) / GetDamage weapon)
			SetNumericGameSetting "fDamageToWeaponMeleeMult", durability

		elseif (GetWeaponSkill weapon == 45)
			set durability to ((baseWeapon + ((100 - Player.GetAV Unarmed) / multWeapon)) * (GetHealth weapon / 100) / GetDamage weapon)
			SetNumericGameSetting "fDamageToWeaponMeleeMult", durability

		elseif (GetWeaponSkill weapon == 41)
			set durability to ((baseWeapon + ((100 - Player.GetAV SmallGuns) / multWeapon)) * (GetHealth weapon / 100) / GetDamage weapon)
			SetNumericGameSetting "fDamageToWeaponGunMult", durability

		elseif (GetWeaponSkill weapon == 34)
			set durability to ((baseWeapon + ((100 - Player.GetAV EnergyWeapons) / multWeapon)) * (GetHealth weapon / 100) / GetDamage weapon)

			; FIX: Energy 'Small Gun'-like
			if (GetWeapType weapon == 3 || GetWeapType weapon == 6)
				SetNumericGameSetting "fDamageToWeaponGunMult", durability
			else
				SetNumericGameSetting "fDamageToWeaponEnergyMult", durability
			endif

		elseif (GetWeaponSkill weapon == 33)
			set durability to ((baseWeapon + ((100 - Player.GetAV BigGuns) / multWeapon)) * (GetHealth weapon / 100) / GetDamage weapon)
			SetNumericGameSetting "fDamageToWeaponLauncherMult", durability

		endif

		set durability to (durability / (GetHealth weapon / 100)) * GetDamage weapon
		dbprintc "Weapon durability: %.5f", durability
	endif

	if (Player.GetArmorRating && Player.GetArmorRating != rating)
		set rating to Player.GetArmorRating
		set armor to Player.GetEqObj 2 ; Upper Body Piece

		if (IsPA armor || GetWeight armor >= 25)
			set durability to baseArmor + ((10 - Player.GetAV Strength) * multArmor)
		elseif (GetWeight armor >= 15)
			set durability to baseArmor + ((10 - Player.GetAV Endurance) * multArmor)
		else
			set durability to baseArmor + ((10 - Player.GetAV Agility) * multArmor)
		endif

		dbprintc "Armor durability: %.5f", durability
		set durability to durability / rating
		SetNumericGameSetting "fDamageToArmorPercentage", durability
	endif
End