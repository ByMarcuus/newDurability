scn newDurabilityLogicScript

ref armor
ref weapon
ref lastWeapon

float durability
float rating
float lastRating
int skill


; Restart the durability at 'LevelUp Menu', 'cause the special/skills can change.
Begin MenuMode 1027
	if (lastWeapon != 0 || lastRating != 0)
		set lastWeapon to 0
		set lastRating to 0
	endif
End

; Script that modifies in-game weapon and armor durability.
Begin GameMode
	set weapon to Player.GetEqObj 5

	if (weapon == 0)
		; pass
	elseif (weapon != lastWeapon)
		set lastWeapon to weapon
		set skill to GetWeaponSkill weapon

		if (skill == 38)
			set durability to Player.GetAV MeleeWeapons
		elseif (skill == 45)
			set durability to Player.GetAV Unarmed
		elseif (skill == 41)
			set durability to Player.GetAV SmallGuns
		elseif (skill == 34)
			set durability to Player.GetAV EnergyWeapons
		elseif (skill == 33)
			set durability to Player.GetAV BigGuns
		else
			set skill to 0
		endif

		if (skill != 0)
			set durability to (GetGlobalVariable fWeaponBaseHealth) + (((100 - durability) / 100) * (GetGlobalVariable fWeaponSkillMult))
			dbprintc "Weapon durability: %.5f", durability
			set durability to durability / GetDamage weapon ; BUG: Float loss precision

			; FIX: Energy 'Small Gun'-like
			; Establishes the durability for all types of weapons.
			; This is done because it seems that some weapons are not attached into the correct 'fDamage*' variable.
			SetNumericGameSetting "fDamageToWeaponGunMult", durability
			SetNumericGameSetting "fDamageToWeaponEnergyMult", durability
			SetNumericGameSetting "fDamageToWeaponMeleeMult", durability
			SetNumericGameSetting "fDamageToWeaponLauncherMult", durability
		endif
	endif

	set rating to Player.GetArmorRating

	if (rating == 0)
		; pass
	elseif (rating != lastRating)
		set lastRating to rating
		set armor to Player.GetEqObj 2 ; Upper Body Piece

		if (IsPA armor || GetWeight armor >= 25)
			set durability to Player.GetAV Strength
		elseif (GetWeight armor >= 15)
			set durability to Player.GetAV Endurance
		else
			set durability to Player.GetAV Agility
		endif

		set durability to (GetGlobalVariable fArmorBaseHealth) + (((10 - durability) / 10) * (GetGlobalVariable fArmorSkillMult))
		dbprintc "Armor durability: %.5f", durability
		SetNumericGameSetting "fDamageToArmorPercentage", durability
	endif
End